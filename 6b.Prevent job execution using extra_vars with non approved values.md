# Preventing unauthorized `extra_vars` values from executing a Job Template

## Overview

In many organizations, the person who launches automation jobs is not
necessarily the same individual who created or maintains the underlying Job
Templates. While Ansible Automation Platform allows users to supply `extra_vars`
at launch time, there's often a need to validate not just the presence of
certain variables, but also the specific values those variables are allowed to accept.

This example demonstrates how to use the Policy as Code feature to enforce a set of approved values for given `extra_vars` variables.

## Example Policy [aap_policy_examples/extra_vars_validation.rego](aap_policy_examples/extra_vars_validation.rego)

The following policy defines which values are valid for specific `extra_vars` variables. If a job is launched with any value outside the allowed list, it will be blocked and an appropriate violation message will be returned.

```rego
package aap_policy_examples

import rego.v1

# Define allowed values for specific keys in extra_vars
valid_extra_var_values := {"extra_var_key": ["allowed_value1", "allowed_value2"]}

# Default policy response indicating allowed status with no violations
default extra_vars_validation := {
	"allowed": true,
	"violations": [],
}

# Evaluate extra_vars_validation to check if extra_vars values are allowed
extra_vars_validation := result if {
	# Extract extra_vars from input, defaulting to empty object if missing
	input_extra_vars := object.get(input, ["extra_vars"], {})

	# Identify keys with disallowed values
	violating_keys := [key |
		valid_extra_var_values[key]
		not allowed_value(key, input_extra_vars[key])
	]

	# Check if there are any violations
	count(violating_keys) > 0

	result := {
		"allowed": false,
		"violations": [sprintf("extra_vars contain disallowed values for keys: %v. Allowed values: %v", [violating_keys, valid_extra_var_values])],
	}
}

# Check if a given value for a key is allowed
allowed_value(key, value) if {
	valid_extra_var_values[key][_] == value
}
```

## Sample Input and Output

Let's say a user launches a job with the following `extra_vars`:

```json
{
  "extra_vars": {
    "extra_var_key": "unauthorized_value"
  }
}
```

The policy is configured to only allow the following values for the `extra_var_key`:

```json
{
  "extra_var_key": ["allowed_value1", "allowed_value2"]
}
```

Because `"unauthorized_value"` is not in that allowed list, the job will be blocked.

Expected policy decision output:

```json
{
  "allowed": false,
  "violations": [
    "extra_vars contain disallowed values for keys: [\"extra_var_key\"]. Allowed values: {\"extra_var_key\": [\"allowed_value1\", \"allowed_value2\"]}"
  ]
}
```


## Enforcement Behavior

This policy is enforced at the Job Template level. Any job launched from a job
template with this policy attached will fail if disallowed `extra_vars` values
provided are not part of the `valid_extra_var_values` variable in the policy.

## Use Case: Enforcing Allowed Values within `extra_vars` Variables

A platform team supports job templates that accept certain `extra_vars` to
control behavior at runtime. For example, users may provide an input like
`deployment_type`, referring to dev or production environments,  to control how
aggressively changes are implemented.

To reduce risk and prevent unintended behavior, the team defines an explicit
list of allowed values for each key—such as `deployment_type: ["dev", "prod"]`.
If a user provides a value not on that list (e.g., `deployment_type:
"staging"`), the job is rejected before it runs.

By enforcing approved values per key, the team ensures only known, supported options are used—preserving consistency and reducing the potential for errors or misuse.

## Impact of Policy Enforcement in Ansible Automation Platform

By enforcing validation, teams can:

- Prevent unsupported or unstable configurations from being introduced into automation
- Ensure only prescribed variable values are used within playbooks
- Reduce debugging time caused by unexpected or invalid user provided values

This gives automation authors more confidence that job behavior will remain predictable, regardless of who launches the job or what values they attempt to pass in.

## How AAP Enforces the Policy

When the policy is attached to a Job Template:

- The `extra_vars` values provided at job launch are evaluated against the policy's allowed value lists
- Jobs are immediately blocked if a variable contains a value outside its defined list
- A clear violation message is returned, identifying which variable(s) failed validation and what values are allowed

This enforcement happens before any playbook execution begins, ensuring violations are caught early in the job lifecycle.

## Why This Matters

Unlike variable restrictions, value validation enforces the correctness of what
users are trying to do, not just what they're allowed to modify. It protects
automation logic that depends on trusted variables by making sure only supported
values are used within those variables.

This is especially important when Job Templates  branch behavior based on
`extra_vars`. A single incorrect value could trigger the wrong deployment
strategy, bypass a safety step, or produce an unpredictable result..

